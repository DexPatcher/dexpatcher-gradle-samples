buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        // Android Gradle plugins:
        classpath 'com.android.tools.build:gradle:3.4.2'
    }
}

buildscript {
    repositories {
        gradlePluginPortal()
        // This is the local repo I use for plugin development, you should remove it:
        flatDir dirs: '../../dexpatcher-gradle/build/libs'
    }
    dependencies {
        // DexPatcher Gradle plugins:
        classpath 'com.github.lanchon.dexpatcher:dexpatcher-gradle:2.1.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

allprojects {
    repositories {
        // DexPatcher repo (https://dexpatcher.github.io/dexpatcher-repo/)
        maven { url 'https://dexpatcher.github.io/dexpatcher-repo/m2' }
        // This is the local repo I use for tool development, you should remove it:
        flatDir dirs: rootProject.file('../../dexpatcher-tool/tool/build/libs')
    }
    dependencies {
        configurations.all {
            switch (name) {
                case 'apktool':
                    add name, 'dexpatcher-repo.ibotpeaches.apktool:apktool:2.4.0'
                    break
                case 'dex2jar':
                    add name, 'dexpatcher-repo.dexpatcher.dex2jar:dex-tools:2.1-20171001-lanchon@zip'
                    break
                case 'dexpatcher':
                    add name, 'dexpatcher-repo.dexpatcher.dexpatcher-tool:dexpatcher:1.7.0'
                    break
                case 'dexpatcherAnnotation':
                    // By default the annotation package bundled within DexPatcher tool is used.
                    //add name, 'dexpatcher-repo.dexpatcher.dexpatcher-tool:dexpatcher-annotation:?'
                    break
                case 'aapt2':
                    // By default the AAPT2 binary provided by the Android Gradle plugin is used
                    // (unless 'useAapt2BundledWithApktool' is set to true in the build script).
                    //add name, 'dexpatcher-repo.ibotpeaches.apktool:apktool-aapt2:?'
                    //add name, 'com.android.tools.build:aapt2:?:linux|windows|osx'
                    //add name, rootProject.files('aapt2/linux|windows|macosx/aapt2[.exe]')
                    break
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
